module class_searchPt
!------------------------------------------------------------------------------
!class_searchPt
!------------------------------------------------------------------------------
!This class contains the derivated data type searchPt as well as the necessary 
!procedures to work with the derivated date type.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
  use mod_param, only: wp,&
                       zero,&
                       one,&
                       two
  use mod_calcEng, only: calc_engAlpha
  private

!------------------------------------------------------------------------------
!Public
!------------------------------------------------------------------------------
  public :: searchPt
  public :: searchPt_init
  public :: searchPt_copy
  public :: searchPt_computeSearchDirection
  public :: searchPt_findHMin
  public :: searchPt_constructPt
  public :: searchPt_SDSConvTest
  public :: searchPt_getPoint
  public :: searchPt_calcEngAlp
  public :: searchPt_displayPt
  public :: searchPt_allocate
  public :: searchPt_deallocate

!------------------------------------------------------------------------------
!Data Type
!------------------------------------------------------------------------------
  type :: searchPt 
     private
     logical :: Lalloc = .false.
     integer :: num
     integer :: calcType
     real(wp), allocatable :: point(:)
  end type searchPt



contains
  

  subroutine searchPt_allocate(num,point)
!------------------------------------------------------------------------------
!searchPt_allocate
!------------------------------------------------------------------------------
!This procedure will allocate the derived data type searchPt
!
!    Date        Programmer        Comments
!    06/10/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Inputs
!------------------------------------------------------------------------------
    integer, intent(in) :: num

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    type(searchPt), intent(out) :: point

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    point%num = num
    if (allocated(point%point)) deallocate(point%point)
    allocate(point%point(num))
    point%Lalloc = .true.

!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_allocate

  subroutine searchPt_deallocate(point,Lalloc)
!------------------------------------------------------------------------------
!searchPt_allocate
!------------------------------------------------------------------------------
!This procedure will allocate the derived data type searchPt
!
!    Date        Programmer        Comments
!    06/10/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Inputs
!------------------------------------------------------------------------------
    type(searchPt), intent(inout) :: point    

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    logical, intent(out) :: Lalloc

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    if(point%Lalloc) then
       Lalloc = point%Lalloc
    else
       stop "***Point unallocated***"
    end if
!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_deallocate

    
  subroutine searchPt_init(num,calcType,point,sPt)
!------------------------------------------------------------------------------
!searchPt_init
!------------------------------------------------------------------------------
!Initalize the searchPt.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Inputs
!------------------------------------------------------------------------------
    integer, intent(in) :: num
    integer, intent(in) :: calcType
    real(wp), intent(in) :: point(num)

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    type(searchPt), intent(out) :: sPt

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    call searchPt_allocate(num,sPt)
    if (sPt%Lalloc) then
       sPt%calcType = calcType
       sPt%point = point
    else
       stop "***Tried to load unallocated point:searchPt_init***"
    end if

!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_init

  subroutine searchPt_displayPt(iout,point)
!------------------------------------------------------------------------------
!searchPt_displayPt
!------------------------------------------------------------------------------
!This procedure displays a the value of a point to the requested output unit.
!
!    Date        Programmer        Comments
!    06/10/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Inputs
!------------------------------------------------------------------------------
    integer, intent(in) :: iout
    type(searchPt), intent(in) :: point

!------------------------------------------------------------------------------
!Internal
!------------------------------------------------------------------------------


!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    write(iout,*) point%point(:)


!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_displayPt

  subroutine searchPt_copy(P1,P2)
!------------------------------------------------------------------------------
!searchPt_copy
!------------------------------------------------------------------------------
!This procedure copies a searchPt.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Input
!------------------------------------------------------------------------------
    type(searchPt), intent(in) :: P1

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    type(searchPt), intent(out) :: P2

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    call searchPt_allocate(P1%num,P2)
    if (P2%Lalloc) then
       call searchPt_init(P1%num,P1%calcType,P1%point,P2)
    else 
       stop "***Failure copying point. Allocatation Error***"
    end if

!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_copy

  subroutine searchPt_add(P1,P2,P3)
!------------------------------------------------------------------------------
!searchPt_add
!------------------------------------------------------------------------------
!This adds two search points together.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Inputs
!------------------------------------------------------------------------------
    type(searchPt), intent(in) :: P1
    type(searchPt), intent(in) :: P2

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    type(searchPt), intent(out) :: P3

!------------------------------------------------------------------------------
!Internal
!------------------------------------------------------------------------------

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    if(P1%num /= P2%num) then
       stop "***Tried to added two searchPt of diffrent lengths***"
    end if
    call searchPt_allocate(P1%num,P3)
    call searchPt_init(P1%num,P1%calcType,P1%point+P2%point, P3)


!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_add

  subroutine searchPt_getNum(Pt,num)
!------------------------------------------------------------------------------
!searchPt_getNum
!------------------------------------------------------------------------------
!This procedure will return the num (length of array.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Input
!------------------------------------------------------------------------------
    type(searchPt), intent(in) :: Pt

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    integer, intent(out) :: num

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    num = Pt%num

!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_getNum

  subroutine searchPt_getPoint(Pt,point)
!------------------------------------------------------------------------------
!searchPt_getNum
!------------------------------------------------------------------------------
!This procedure will return the num (length of array.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Input
!------------------------------------------------------------------------------
    type(searchPt), intent(in) :: Pt

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    real(wp), intent(out) :: point(Pt%num)

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    point = Pt%num

!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_getPoint

  subroutine searchPt_scalarMult(scalar,oldPt,newPt)
!------------------------------------------------------------------------------
!searchPt_scalarMult
!------------------------------------------------------------------------------
!This procedure will multiply a scalar with the vector in the searchPt.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Input
!------------------------------------------------------------------------------
    real(wp), intent(in) :: scalar
    type(searchPt), intent(in) :: oldPt

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    type(searchPt), intent(out) :: newPt

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    call searchPt_copy(oldPt,newPt)
    newPt%point = scalar*(newPt%point)

!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_scalarMult

  subroutine searchPt_add2Elem(pos,val,oldPt,newPt)
!------------------------------------------------------------------------------
!searchPt_add2Elem
!------------------------------------------------------------------------------
!This procedure will alter a specific element in the vector by adding a given
!real to the position.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Input
!------------------------------------------------------------------------------
    integer, intent(in) :: pos
    real(wp), intent(in) :: val
    type(searchPt), intent(in) :: oldPt

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    type(searchPt), intent(out) :: newPt

!------------------------------------------------------------------------------
!Internal
!------------------------------------------------------------------------------

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    if (pos > OldPt%num) then
       stop "***Tried to add to non-existant element in searchPt***"
    end if
    
    call searchPt_copy(oldPt,newPt)
    newPt%point(pos) = (newPt%point(pos) + val)

!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_add2Elem

  subroutine searchPt_calcEngAlp(point,eng)
!------------------------------------------------------------------------------
!searchPt_calcEngAlp
!------------------------------------------------------------------------------
!This procedure will return the energy from the evaluation of the point as 
!a set of coeffiecents for a basis set.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Inputs
!------------------------------------------------------------------------------
    type(searchPt), intent(in) :: point

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    real(wp), intent(out) :: eng

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    call calc_engAlpha(point%num,point%calcType, point%point, eng)
!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_calcEngAlp

  subroutine searchPt_GradVec(h,point,gradVec)
!------------------------------------------------------------------------------
!searchPt_GradVec
!------------------------------------------------------------------------------
!This procedure will calculate a gradient vector from a point.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Input
!------------------------------------------------------------------------------
    real(wp), intent(in) :: h
    type(searchPt), intent(in) :: point

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    type(searchPt), intent(out) :: gradVec

!------------------------------------------------------------------------------
!Internals
!------------------------------------------------------------------------------
    integer :: i

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    call searchPt_copy(point,gradVec)
    do i=1, point%num
       call centDifDerAlp(i,h,point,gradVec%point(i))
    end do

!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_GradVec

  subroutine centDifDerAlp(ipos,h,point,ans)
!------------------------------------------------------------------------------
!centDifDerAlp
!------------------------------------------------------------------------------
!This procedure calculates the derivative for the point with the respective 
!variable in the ipos using the searchPt_calcEngAlp subroutine.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Input
!------------------------------------------------------------------------------
    integer, intent(in) :: ipos
    real(wp), intent(in) :: h
    type(searchPt), intent(in) :: point

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    real(wp), intent(out) :: ans

!------------------------------------------------------------------------------
!Internal
!------------------------------------------------------------------------------
    real(wp) :: sum
    real(wp) :: diff
    type(searchPt) :: Plus
    type(searchPt) :: xMinus
!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    call searchPt_add2Elem(ipos,h,point,Plus)
    call searchPt_add2Elem(ipos,-h,point,xMinus)
    call searchPt_calcEngAlp(Plus, sum)
    call searchPt_calcEngAlp(xMinus,diff)
    ans = (sum-diff)/(two*h)


!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine centDifDerAlp

  subroutine searchPt_norm(point, ans)
!------------------------------------------------------------------------------
!searchPt_norm
!------------------------------------------------------------------------------
!This subroutine will calculate the norm of a point vector.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Input
!------------------------------------------------------------------------------
    type(searchPt), intent(in) :: point

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    real(wp), intent(out) :: ans

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    ans = sqrt(dot_product(point%point,point%point))

!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_norm

  subroutine searchPt_computeSearchDirection(h,point,searchVec)
!------------------------------------------------------------------------------
!searchPt_computeSearchDirection(point,searchVec)
!------------------------------------------------------------------------------
!This procedure will compute a steepest descent search vector.
!See p412 Matthews and Fink Numerical Analysis
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Input
!------------------------------------------------------------------------------
    real(wp), intent(in) :: h
    type(searchPt), intent(in) :: point

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    type(searchPt), intent(out) :: searchVec

!------------------------------------------------------------------------------
!Internals
!------------------------------------------------------------------------------
    real(wp) :: xnorm
    type(searchPt) :: GradVec

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    call searchPt_GradVec(h,point,GradVec)
    call searchPt_norm(GradVec, xnorm)
    call searchPt_scalarMult((-one/xnorm), GradVec,searchVec)

!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_computeSearchDirection

  subroutine searchPt_findHMin(stepSize,a,b,point,searchVec,hmin)
!------------------------------------------------------------------------------
!searchPt_findHMin
!------------------------------------------------------------------------------
!This procedure will perform the single parameter search to find the optimal 
!hmin.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Orignal Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Inputs
!------------------------------------------------------------------------------
    real(wp), intent(in) :: stepSize
    real(wp), intent(in) :: a
    real(wp), intent(in) :: b
    type(searchPt), intent(in) :: point
    type(searchPt), intent(in) :: searchVec

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    real(wp), intent(out) :: hmin

!------------------------------------------------------------------------------
!Internals
!------------------------------------------------------------------------------
    real(wp) :: temp
    real(wp) :: best
    real(wp) :: eng
    type(searchPt) :: tempSV
    type(searchPt) :: tempPoint

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    temp = a
    best = 999.0e15_wp
    do 
       if (temp .gt. b) EXIT
       call searchPt_constructPt(temp,searchVec,point,tempPoint)
       call searchPt_calcEngAlp(tempPoint, eng)
       if (eng < best) then
          best = eng
          hmin = temp
       end if
       temp = temp + stepSize
    end do

!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_findHMin

  subroutine searchPt_constructPt(scalar,mPt,bPt,newPt)
!------------------------------------------------------------------------------
!searchPt_constructPt
!------------------------------------------------------------------------------
!This procedure constructs a point as newPt = scalar*mPt + bPt
!The m and b are mnemonic for the linear form.
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Inputs
!------------------------------------------------------------------------------
    real(wp), intent(in) :: scalar
    type(searchPt), intent(in) :: mPt
    type(searchPt), intent(in) :: bPt

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    type(searchPt), intent(out) :: newPt

!------------------------------------------------------------------------------
!Internals
!------------------------------------------------------------------------------
    type(searchPt) :: temp

!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    call searchPt_scalarMult(scalar,mPt,temp)
    call searchPt_add(bPt,temp,newPt)

!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_constructPt

  subroutine searchPt_SDSConvTest(funcTol, distTol, oldPt,newpt,stats, conv)
!------------------------------------------------------------------------------
!searchPt_SDSConvTest
!------------------------------------------------------------------------------
!This procedure will calculate the convergence of the steepest descent search 
!and return conv = .t. if abs(f(newPt)-f(oldPt)) <= funcTol and if
! norm(newPt-oldPt) <= distTol
!
!    Date        Programmer        Comments
!    06/09/2011  C. Blanton        Original Code
!
!------------------------------------------------------------------------------
    implicit none

!------------------------------------------------------------------------------
!Inputs
!------------------------------------------------------------------------------
    real(wp), intent(in) :: funcTol
    real(wp), intent(in) :: distTol
    type(searchPt), intent(in) :: oldPt
    type(searchPt), intent(in) :: newPt

!------------------------------------------------------------------------------
!Output
!------------------------------------------------------------------------------
    logical, intent(out) :: conv
    real(wp), intent(out) :: stats(2)
!------------------------------------------------------------------------------
!Internals
!------------------------------------------------------------------------------
    real(wp) :: temp1
    real(wp) :: temp2
    real(wp) :: diff
    real(wp) :: dist
    type(searchPt) :: tempPoint
!------------------------------------------------------------------------------
!Begin Code
!------------------------------------------------------------------------------
    call searchPt_calcEngAlp(oldPt,temp1)
    call searchPt_calcEngAlp(newPt,temp2)
    diff = abs(temp2-temp1)
    call searchPt_constructPt((-one),oldPt,newPt,tempPoint)
    call searchPt_norm(tempPoint,dist)
    stats(1) = diff
    stats(2) = dist
    if ((diff < funcTol) .and. (dist < distTol)) then
       conv = .true.
    else
       conv = .false.
    end if

!------------------------------------------------------------------------------
!End Code
!------------------------------------------------------------------------------
  end subroutine searchPt_SDSConvTest

end module class_searchPt
